plugins {
  id 'org.jetbrains.intellij' version "0.0.3"
}

version = '1.0'

allprojects {
  apply plugin: 'org.jetbrains.intellij'
  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  idea {
    version '14.1'
    plugins 'coverage'
  }

  sourceSets {
    main {
      java.srcDirs 'src', 'gen'
      resources.srcDir 'resources'
    }
    test {
      java.srcDir 'tests'
    }
  }
}

dependencies {
  compile project(':utils'), project(':google-app-engine'), project(':google-app-engine-yaml')
}

task performanceTest(type: Test) {
  useJUnit {
    includeCategories 'com.goide.categories.Performance'
    reports.html.destination = "$buildDir/reports/performanceTests"
  }
}
check.dependsOn performanceTest

test {
  maxHeapSize = '512m'
  minHeapSize = '256m'
  enableAssertions = true
  jvmArgs '-XX:MaxPermSize=250m', '-Didea.system.path=system-test', '-Didea.config.path=config-test'

  useJUnit {
    excludeCategories 'com.goide.categories.Performance'
  }
}